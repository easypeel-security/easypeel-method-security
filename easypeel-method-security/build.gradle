apply plugin: 'java'
apply plugin: 'java-library'
pluginManager.apply("io.github.goooler.shadow")
//pluginManager.apply("com.github.johnrengelman.shadow")
pluginManager.apply("com.vanniktech.maven.publish")

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation "com.squareup:javapoet:${javapoetVersion}"
    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"
}

shadowJar {
    archiveBaseName.set(easypeelMethodSecurity)
    archiveVersion.set(easypeelMethodSecurityVersion)
    archiveClassifier.set("")
    archiveExtension.set("jar")

    relocate("com.squareup.javapoet", "org.epsec.javapoet")
    relocate("com.github.benmanes.caffeine", "org.epsec.caffeine")
}

jar.enabled = false

publishing {
    publications {
        shadow(MavenPublication) {
            artifact shadowJar
            from components.java
        }
    }
}


mavenPublishing {
    coordinates(rootPackageName, easypeelMethodSecurity, easypeelMethodSecurityVersion)

    pom {
        name = easypeelMethodSecurity
        description = 'Configure method-level security easily in your RestController'
        inceptionYear = '2024'
        url = 'https://github.com/easypeel-security/easypeel-method-security/'

        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id = 'PENEKhun'
                name = 'MoonSeonghun'
                email = 'penekhun@gmail.com'
                url = 'https://github.com/PENEKhun'
            }
        }

        scm {
            connection = 'scm:git:git://github.com/easypeel-security/easypeel-method-security.git'
            developerConnection = 'scm:git:ssh://github.com/easypeel-security/easypeel-method-security.git'
            url = 'https://github.com/easypeel-security/easypeel-method-security'
        }
    }
}
