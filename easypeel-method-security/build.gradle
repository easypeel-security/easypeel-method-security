plugins {
    id 'com.vanniktech.maven.publish' version "0.28.0-rc1"
    id 'io.github.goooler.shadow' version '8.1.6'
    //	id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java-library'
}


group = rootPackageName
version = easypeelMethodSecurityVersion

java {
    sourceCompatibility = JavaVersion.VERSION_17
}


tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

tasks.register('sourcesJarBuild', Jar) {
    dependsOn classes
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJarBuild
    archives jar
}

dependencies {
    implementation "com.squareup:javapoet:${javapoetVersion}"
    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"

    compileOnly "org.springframework.boot:spring-boot-starter:${springVersion}"
}

shadowJar {
    archiveBaseName.set(easypeelMethodSecurity)
    archiveVersion.set(easypeelMethodSecurityVersion)
    archiveClassifier.set("")
    archiveExtension.set("jar")
    mergeServiceFiles()

    relocate("com.squareup.javapoet", rootPackageName + ".javapoet")
    relocate("com.github.benmanes.caffeine", rootPackageName + ".caffeine")
}

mavenPublishing {
    coordinates(rootPackageName, easypeelMethodSecurity, easypeelMethodSecurityVersion)

    pom {
        name = 'Easypeel Method Security'
        description = 'Configure method-level security easily in your RestController'
        inceptionYear = '2024'
        url = 'https://github.com/easypeel-security/easypeel-method-security/'

        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id = 'PENEKhun'
                name = 'MoonSeonghun'
                email = 'penekhun@gmail.com'
                url = 'https://github.com/PENEKhun'
                group = 'Owner'
                organization {
                    name = 'easypeel-security'
                    url = 'https://github.com/easypeel-security'
                }
            }
        }

        scm {
            connection = 'scm:git:git://github.com/easypeel-security/easypeel-method-security.git'
            developerConnection = 'scm:git:ssh://github.com/easypeel-security/easypeel-method-security.git'
            url = 'https://github.com/easypeel-security/easypeel-method-security'
        }
    }
}

build {
    dependsOn shadowJar
}

